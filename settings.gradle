def commandDir = System.getProperty("user.dir")

if (rootProject.dir.toString() != commandDir.toString()) {
	pluginsIncludes = commandDir.toString().replaceAll(".+[\\\\/]", "")
}

String[] pluginsExcludesList =
	pluginsExcludes.replaceAll(" ", "").replaceAll(",+", ",").split(",")

String[] pluginsIncludesList =
	pluginsIncludes.replaceAll(" ", "").replaceAll(",+", ",").split(",")

FileTree projectScripts = fileTree(rootDir) {
	if (pluginsIncludes != "" && pluginsIncludes != "*") {
		String[] projectScriptsIncludes = pluginsIncludesList.collect {
			"**/" + it + "/build.gradle"
		}

		projectScriptsIncludes.each { 
			include it
		}
	}
	else {
		include "**/build.gradle"
	}

	if (pluginsExcludes != "") {
		String[] projectScriptsExcludes = pluginsExcludesList.collect {
			"**/" + it + "/build.gradle"
		}

		projectScriptsExcludes.each {
			exclude it
		}
	}

	exclude "build.gradle"	//exclude root
}

def addProject

addProject = {
	include it

	String projectPropertiesFileStr

	projectPropertiesFileStr = it.replaceAll(":", "/")
	projectPropertiesFileStr = projectPropertiesFileStr + "/gradle.properties"
	projectPropertiesFileStr = projectPropertiesFileStr.replaceFirst("^/", "")
	projectPropertiesFileStr = rootDir.toString() + "/" + projectPropertiesFileStr

	File projectPropertiesFile = new File(projectPropertiesFileStr)

	if (projectPropertiesFile.exists()) {
		Properties projectProperties = new Properties()

		projectProperties.load(new FileInputStream(projectPropertiesFile))

		projectProperties.dependencyProjects.split(",").each {
			addProject(it)
		}
	}
}

projectScripts.each {
	URI projectScriptUri = it.toURI()
	URI rootDirUri = rootDir.toURI()
	URI projectScriptRelativeUri = rootDirUri.relativize(projectScriptUri)
	String projectScriptRelativeStr = projectScriptRelativeUri.toString()
	String projectDirRelativeStr =
		projectScriptRelativeStr.replaceFirst("/build.gradle", "")
	String projectPath = projectDirRelativeStr.replaceAll("[\\/]", ":")

	addProject(projectPath)
}